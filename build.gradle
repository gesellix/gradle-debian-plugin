apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'versions'

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = 14
group = 'de.gesellix'
archivesBaseName = 'gradle-debian-plugin'

buildscript {
  repositories {
    maven {
      url 'http://jcenter.bintray.com'
    }
    maven {
      url 'http://kt3k.github.io/repository/maven/release'
    }
    maven {
      url "https://github.com/ben-manes/gradle-versions-plugin/raw/mvnrepo"
    }
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.5-beta-1'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'

    classpath 'net.saliman:gradle-cobertura-plugin:1.1.2'
//    classpath 'net.saliman:gradle-cobertura-plugin:1.2.1'
//    classpath 'net.saliman:gradle-cobertura-plugin:2.1.0'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.1.5'
//    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.1.6'
  }
}
apply plugin: 'bintray'

apply plugin: 'cobertura'
apply plugin: 'coveralls'

cobertura.coverageFormats = ['html', 'xml']
cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

repositories {
  mavenCentral()
}

configurations.all {
  resolutionStrategy {
    failOnVersionConflict()

    def dependencyVersions = [
        'org.codehaus.plexus:plexus-classworlds': '1.2-alpha-9',
        'org.codehaus.plexus:plexus-container-default': '1.0-alpha-30',
        'org.codehaus.plexus:plexus-interactivity-api': '1.0-alpha-6',
        'org.codehaus.plexus:plexus-utils': '1.5.15',
        'classworlds:classworlds': '1.1',
        'commons-httpclient:commons-httpclient': '3.1',
        'commons-io:commons-io': '2.4',
        'commons-codec:commons-codec': '1.6',
        'org.hamcrest:hamcrest-core': '1.3',
        'org.slf4j:slf4j-nop': '1.7.2',
        'org.slf4j:slf4j-jdk14': '1.7.2',
        'org.slf4j:slf4j-api': '1.7.2',
        'org.slf4j:jcl-over-slf4j': '1.7.2',
        'org.apache.ant:ant': '1.9.2',
        'org.apache.ant:ant-launcher': '1.9.2',
        'junit:junit': '4.11',
        'xerces:xmlParserAPIs': '2.6.2',
        'xerces:xercesImpl': '2.6.2',
        'xml-apis:xml-apis': '2.0.2'
    ]

    force dependencyVersions.collect { k, v -> "$k:$v" }
  }
}

dependencies {
  compile gradleApi()
  compile localGroovy()
//  groovy 'org.codehaus.groovy:groovy-all:2.0.5'

  compile 'org.vafer:jdeb:1.1'
  compile 'commons-lang:commons-lang:2.6'

  testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
  testCompile 'cglib:cglib-nodep:2.2.2'
}

task testng(type: Test) {
  useTestNG()
}
check.dependsOn testng

/*task testReport(type:TestReport) {
  reportOn test, testng
}*/

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: 'file:./lib')
    }
  }
}

publishing {
  publications {
    plugin(MavenPublication) {
      from components.java  // jar
    }
  }
}

ext.dry = false
bintray {
  user = project["bintray.user"]
  key = project["bintray.key"]
  publications = ['plugin']
  pkg {
    repo = 'gradle-plugins'
    //userOrg = 'myorg' // an optional organization name when the repo belongs to one of the user's orgs
    name = 'gradle-debian-plugin'
    desc = 'create debian packages with gradle'
    licenses = ['Apache-2.0']
    labels = []
  }
  dryRun = dry
}
bintrayUpload.dependsOn(":build")

task wrapper(type: Wrapper) {
  gradleVersion = '1.11'
  distributionUrl = 'http://services.gradle.org/distributions/gradle-1.11-all.zip'
}
